REM : Skrypt wklajmy w putty po połączeniu z drukarką (115200,8,1,none)
REM : W drukarce wczesniej nalezy włączyc virtualny port COM
REM :

DELETE1-100000
IMMEDIATE OFF

'
' Wyswietlanie klawiszy na ekranie drukarki
'
011 DISPLAY KEY 1,0
012 DISPLAY KEY 2,0
013 DISPLAY KEY 3,0
014 DISPLAY KEY 4,0
015 DISPLAY KEY 5,5
016 
017 '
018 ' Deklaracja zmiennych 
019 '
020 
021 'wartosc do przekazania z klawiatury
022 A$="15" 
023 'cena do druku
024 C$=A$
025 'tmp do edycji
026 T$="" 
027 'tmp do wyswietlania
028 D$=A$ 
029 I%=0
030 L%=1
031 '
032 ' Aktywacja klawiszy drukarki 
033 '
034 KEY (10) ON
035 KEY (11) ON
036 KEY (12) ON
037 KEY (13) ON
038 KEY (14) ON
039 KEY (16) ON
040 KEY (17) ON
041 KEY (20) ON
042 
043 '
044 ' Aktywacja wyswietlacza i klawiatury USB 
045 '
046 OPEN "console:" FOR OUTPUT AS 1
047 COMSET 9,"","/","","=",1
048 ON COMSET 9 GOSUB SUBREAD
049 
050 
051 '
052 ' Aktywacja zdarzen dla klawiszy drukarki i obsluga bledów 
053 '
054 ON KEY (10) GOSUB SUBHOME
055 ON KEY (12) GOSUB SUBUP
056 ON KEY (14) GOSUB SUBDOWN
057 ON KEY (11) GOSUB SUBLEFT
058 ON KEY (13) GOSUB SUBRIGHT
059 ON KEY (16) GOSUB SUBENTER
060 ON KEY (20) GOSUB SUBBACK
061 ON KEY (17) GOSUB SUBPAPER
062 ON ERROR GOTO SUBERROR
063 
064 'SETSTDIO 9,0
065 PRINT #1, CHR$(155) + "2J"
066 FORMFEED
067 
068 ' Wylaczenie domyslnego dzialania klawisza wstecz
069 BREAK 0 OFF
070 ' Obsługa UTF-8
071 NASC 8
072 ' Wlaczenie czujnika dyspensera
073 LTS& ON
074 
075 GOSUB SUBSCREEN
076 
077 '
078 ' Glowna petla czekajca na wprowadzenie danych
079 MAINLOOP:
080 COMSET 9 ON
081 IF 1 THEN GOTO MAINLOOP
082 
083 '
084 ' Wprowadzenia danych z USB
085 SUBREAD:
086 Z$ = COMBUF$(9)
087 ' jesli ENTER
088 IF Z$=CHR$(10) THEN 
089 'PRINT "ENTER"
090 A$=T$ 
091 D$=T$
092 T$=""
093 
094 IF A$<>"" THEN C$=A$
095 
096 X$="ARRAY$"
097 DIM ARRAY$(1)
098 I%=SPLIT(A$,X$,42)
099 IF I%=2 THEN 
100 L%=VAL(ARRAY$(0))
101 C$=ARRAY$(1)
102 ELSE 
103 L%=1 
104 ENDIF
105 D$=C$
106 GOSUB SUBPRINT
107 ' jesli BACKSPACE
108 ELSE IF Z$=CHR$(8) THEN 
109 T$ = LEFT$(T$,LEN(T$)-1) : D$=T$
110 ' jesli znak
111 ELSE 
112 T$=T$+Z$
113 D$=T$
114 ENDIF
115 GOTO SUBSCREEN
116 RETURN
117 
118 SUBSCREEN:
119 'PRINT #1, CHR$(155) + "2J"
120 PRINT #1, "Cena: ";+D$
121 'PRINT #1
122 RETURN
123 
124 
125 SUBPRINT:
126 'PRINT PRSTAT
127 IF (PRSTAT AND 2) THEN 
128 PRINT #1, "Zabierz etyk."
129 ZABIERZETYK: IF (PRSTAT AND 2)<>0 THEN GOTO ZABIERZETYK
130 ENDIF
131 IF (PRSTAT AND 4) OR (PRSTAT AND 128) THEN PRINT #1, "Sprawdz_papier"
132 SPRAWPAPIER: IF (PRSTAT AND 4)<>0 OR (PRSTAT AND 128)<>0 THEN GOTO SPRAWPAPIER
133 
134 PRPOS 10,160
135 FONT "Univers",7
136 PRTXT "Dyskont TANIA ODZIEŻ"
137 
138 PRPOS 10,60
139 FONT "Univers Bold",30,0,75
140 PRTXT C$;" zł"
141 
142 PRPOS 10,35
143 FORMAT DATE$ "YYYY-MM-DD"
144 FORMAT TIME$ "HH:MM:SS" 
145 FONT "Univers",7
146 PRTXT "Data: ";DATE$("F");" ";TIME$("F") 
147 
148 PRPOS 10,10
149 FONT "Univers",7
150 PRTXT "Stanowisko 1"
151 
152 PRINTFEED L%
153 RETURN
154 
155 SUBHOME: 
156 BEEP
157 RETURN
158 
159 
160 SUBUP: 
161 BEEP
162 RETURN
163 
164 SUBDOWN: 
165 TESTFEED
166 RETURN
167 
168 SUBLEFT: 
169 BEEP
170 RETURN
171 
172 SUBRIGHT: 
173 BEEP
174 RETURN
175 
176 SUBENTER: 
177 BEEP
178 RETURN
179 
180 SUBBACK: 
181 PRINT "Bye"
182 LTS& OFF
183 CLOSE
184 'SETSTDIO 100
185 END
186 
187 SUBPAPER:
188 IF PRSTAT<7 THEN 
189 'PRINT "JEST OK"
190 PRINT PRSTAT
191 FORMFEED
192 RETURN
193 ELSE 
194 'PRINT #1, CHR$(155) + "2J"
195 PRINT #1, "Sprawdz papier"
196 'PRINT #1, "===============";
197 'PRINT PRSTAT
198 RETURN
199 ENDIF
200 'czyszczenie ekranu
201 GOSUB SUBSCREEN
202 RETURN


9400 SUBERROR:
9500 PRINT #1, "Blad ";ERR; " ";ERL
9530 IF ERR=1005 OR ERR=1031 THEN
9532 PRINT #1, "Sprawdz papier"
9550 IF (PRSTAT AND 1)=0 THEN GOTO 9550
9580 ENDIF
9590 IF ERR=1022 THEN
9591 PRINT #1, "Podn.glowica"
9592 ENDIF
9600 IF ERR=1003 THEN RESUME NEXT
9650 IF (PRSTAT AND 1)<>0 THEN GOTO 9650
9670 RESUME NEXT

IMMEDIATE ON

SAVE "tmp:ETYKNEW.PRG"

SYSVAR(43)=1
COPY "/tmp/ETYKNEW.PRG","/c/scripts/ETYKNEW.PRG"
COPY "/tmp/ETYKNEW.PRG","/c/ETYKNEW.PRG"
OPEN "AUTOEXEC.BAT" FOR OUTPUT AS 1
PRINT#1,"RUN";CHR$(34);"ETYKNEW.PRG";CHR$(34)
CLOSE1

'RUN
'RUN "ETYKNEW.PRG"
REBOOT

REM :## Czyszczenie pliku autostartu
REM :OPEN "AUTOEXEC.BAT" FOR OUTPUT AS 1
REM :PRINT#1,""
REM :CLOSE1





